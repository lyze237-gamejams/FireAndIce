buildscript {
	repositories {
		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}
	dependencies {
		classpath "org.beryx:badass-runtime-plugin:1.8.5"
	}
}

apply plugin: 'application'
apply plugin: 'org.beryx.runtime'

sourceSets.main.resources.srcDirs += [ rootProject.file('assets').path ]
mainClassName = 'dev.lyze.parallelworlds.lwjgl3.Lwjgl3Launcher'
sourceCompatibility = 11.0

dependencies {
	implementation project(':core')
	implementation "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
	implementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
	implementation "de.golfgl.gdxcontrollerutils:gdx-controllers-jamepad:$controllerUtilsVersion"
}

import org.gradle.internal.os.OperatingSystem

run {
	workingDir = rootProject.file('assets').path
	setIgnoreExitValue(true)
	
	if (OperatingSystem.current() == OperatingSystem.MAC_OS) {
		// Required to run LWJGL3 Java apps on MacOS
		jvmArgs += "-XstartOnFirstThread"
	}
}
jar {
	destinationDirectory = file("$buildDir/lib")
	archiveFileName = "${appName}-${archiveVersion.get()}.jar"
	//// These can excluded because they add to the jar size but libGDX 1.9.11 can't use them.
	//// If your libGDX version is 1.9.10 or earlier, or is 1.9.12-SNAPSHOT or later, you can leave
	//// the following line commented; if you use 1.9.11 exactly, or if you use 1.9.12-SNAPSHOT but
    //// don't need ARM Linux support, you can uncomment it safely.
	//// It's always safe to keep that line commented out; it only affects JAR size.
	//exclude('linux/arm32/**', 'linux/arm64/**')
	dependsOn configurations.runtimeClasspath
	from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } } 
	manifest {
		attributes 'Main-Class': project.mainClassName
	}
}

jpackageImage.dependsOn jar

runtime {
	options = ['--strip-debug',
			   '--compress', '2',
			   '--no-header-files',
			   '--no-man-pages',
			   '--strip-native-commands',
			   '--vm', 'server']
	modules = ['java.base' ,
			   'java.desktop',
			   'jdk.unsupported']

	jpackage {
		installerOptions += [ '--vendor', 'lyze.dev', '--description', 'Libgdx gamejam project september 2020' ]
		mainJar = jar.archiveFileName.get()
		installerName = imageName = appName

		if (OperatingSystem.current().windows) {
			imageOptions += [ '--win-console'] //, "--icon", file("../assets/icons/Icon.ico") ]
			installerOptions += ['--win-per-user-install', '--win-dir-chooser', '--win-menu', '--win-shortcut', '--win-upgrade-uuid', '384b3846-8585-45d1-a1e3-5e5145019591']
		} else {
			//imageOptions += ["--icon", file("../assets/icons/Icon-32.png")]
			installerOptions += ['--linux-menu-group', '--linux-shortcut']
		}
	}
}
