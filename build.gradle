buildscript {
	repositories {
		mavenLocal()
		mavenCentral()
		google()
		maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
		jcenter()
	}
	dependencies {
		classpath "org.wisepersist:gwt-gradle-plugin:$gwtPluginVersion"
		classpath "com.badlogicgames.gdx:gdx-tools:$gdxVersion"
	}
}

def getGitHash = { ->
	def stdout = new ByteArrayOutputStream()
	exec {
		commandLine 'git', 'rev-list', '--count', 'HEAD'
		standardOutput = stdout
	}
	return stdout.toString().trim()
}

import com.badlogic.gdx.tools.texturepacker.TexturePacker
task texturePacker {
	doLast {
		TexturePacker.process("${project.rootDir}/assets/images/particles", "${project.rootDir}/assets/atlas", "particles")
		TexturePacker.process("${project.rootDir}/assets/images/ui", "${project.rootDir}/assets/atlas", "ui")
	}
}

configure(subprojects) {
	apply plugin: 'java-library'
	sourceCompatibility = 11.0
	compileJava {
		options.incremental = true
	}

	compileJava.dependsOn texturePacker
}

subprojects {
	version = '0.0.' + getGitHash()
	ext.appName = 'ParallelWorlds'
	repositories {
		mavenLocal()
		mavenCentral()
		maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
		maven { url 'https://jitpack.io' }
		jcenter()
	}
}
